IGNORE : "[ \f\t\n\r]+";

TRUENAME : "TRUE";
FALSENAME : "FALSE";
NUMBERLITERAL : "(((0|[1-9][0-9]*)(\.[0-9]*)?)|(\.[0-9]+))";
INTLITERAL : "(0|[1-9][0-9]*)[lL]";
IDENTIFIERNAME : "[a-zA-Z_][a-zA-Z0-9_]*";
STRINGLITERAL : "\"([^\"\\]|\\.)*\"";

main : sourceelements [EOF];

sourceelements : sourceelement >sourceelements<
               | sourceelement
               ;

sourceelement : <statement>
              ;

statement : <block>
          | <assignmentexpression>
          | <ifstatement>
          | <whilestatement>
          | <printstatement>
          | <logicalexpression>
          | <pythonexpression>
          ;

block : ["{"] (statement)* ["}"]
      ;

muloperator : "*" | "/";
multiplicativeexpression : primaryexpression (>muloperator< primaryexpression)*
                         ;

addoperator : "+" | "-";
additiveexpression : multiplicativeexpression (>addoperator< multiplicativeexpression)*
                   ;

assignmentoperator : "<-";
assignmentexpression : identifier >assignmentoperator< additiveexpression
                     ;

pythonexpression : ["python"] ["("] literal [")"];

equalityoperator : "==" | "!=";
equalityexpression : relationalexpression (>equalityoperator< relationalexpression)*
                   ;

relationalop : "<=" | ">=" | "<" | ">";
relationalexpression : additiveexpression (>relationalop< additiveexpression)*
                     ;

logicalexpression : logicalor
                  ;

orop : "||";
logicalor : logicaland ( >orop< logicaland )*;

andop : "&&";
logicaland : equalityexpression ( >andop< equalityexpression )*;

ifstatement : ["if"] ["("] logicalexpression [")"] block ["else"] block
            | ["if"] ["("] logicalexpression [")"] block
            ;

whilestatement : ["while"] ["("] logicalexpression [")"] block
               ;

printstatement : ["print"] ["("] literal [")"]
               ;

primaryexpression : <literal>
                  | <vector>
                  | "(" <logicalexpression> ")"
                  ;

vector : ["c"] ["("] elementlist [")"]
       ;

elementlist : literal ([","] literal)* 
            ;

literal : booleanliteral
        | number
        | identifier 
        | string
        ;

identifier : IDENTIFIERNAME;

string: STRINGLITERAL;

number : NUMBERLITERAL
       | INTLITERAL
       ;

booleanliteral : TRUENAME
               | FALSENAME
               ;
