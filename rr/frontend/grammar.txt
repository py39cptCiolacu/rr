IGNORE : "[ \f\t\n\r]+";

NUMBERLITERAL: "(((0|[1-9][0-9]*)(\.[0-9]*)?)|(\.[0-9]+))";
IDENTIFIERNAME: "[a-zA-Z_][a-zA-Z0-9_]*";

main : sourceelements [EOF];

sourceelements : sourceelement >sourceelements<
               | sourceelement
               ;

sourceelement : <statement>
              ;

statement : <equalityexpression>  
          | <assignmentexpression>
          | <additiveexpression>
          | <ifstatement>
          | <forstatement>
          | <printstatement>
          ;

muloperator : "*" | "/";
multiplicativeexpression : literal (>muloperator< literal)+
                         | primaryexpression
                         ;

addoperator : "+" | "-";
additiveexpression : multiplicativeexpression (>addoperator< multiplicativeexpression)+
                   | <multiplicativeexpression>
                   ;

assignmentoperator : "<-";
assignmentexpression : literal >assignmentoperator< additiveexpression
                     ;

equalityoperator : "==" | "!=";
equalityexpression : literal >equalityoperator< literal
                   ;

ifstatement : ["if"] ["("] equalityexpression [")"] ["{"] statement ["}"] ["else"] ["{"] statement ["}"]
            | ["if"] ["("] equalityexpression [")"] ["{"] statement ["}"] 
            ;

forstatement : ["for"] ["("] identifier [")"] ["in"] number [":"] number ["{"] statement ["}"];

printstatement : ["print"] ["("] literal [")"]
               ; 

primaryexpression : <literal>
                  | <vector>
                  ;

vector : ["c"] ["("] elementlist [")"]
       ;

elementlist : literal ([","] literal)* 
            ;

literal : identifier 
        | number
        | "(" <additiveexpression> ")"
        ;

identifier : IDENTIFIERNAME;
number : NUMBERLITERAL;
